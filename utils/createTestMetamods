#!/usr/bin/perl -w
# This code is a part of Slash, and is released under the GPL.
# Copyright 1997-2001 by Open Source Development Network. See README
# and COPYING for more information, or see http://slashcode.com/.
# $Id$

use strict;
use File::Basename;
use FindBin '$Bin';
use Getopt::Std;
use Slash;
use Slash::Utility;
use Benchmark;

use vars qw( $slashdb $constants );

(my $VERSION) = ' $Revision$ ' =~ /\$Revision:\s+([^\s]+)/;
my $PROGNAME = basename($0);
(my $PREFIX = $Bin) =~ s|/[^/]+/?$||;

my (%opts);
# Remember to doublecheck these match usage()!
usage('Options used incorrectly') unless getopts('hu:v', \%opts);
usage() if ($opts{'h'} || !keys %opts);
version() if $opts{'v'};
	
$opts{'u'} ||= 'slash';
$opts{num_closed} = $ARGV[0] || 10;
usage('Invalid number of meta-moderations') 
	if $opts{num_closed} !~ /^\d+$/ || $opts{num_mods} < 0;

createEnvironment($opts{u});
$slashdb = getCurrentDB();
$constants = getCurrentStatic();

# Insure we don't exceed the maximum number of moderations that can be made.
{
	my $max_mods = $slashdb->sqlSelect(
		'count(*)',
		'moderatorlog',
		"m2count < $constants->{m2_consensus}"
	);
	if ($opts{num_closed} > $max_mods) {
		$opts{num_closed} = $max_mods ;
		print "- Count set to maximum number of mods, $max_mods\n";
	}
}

# main program logic (in braces to offset nicely)
{
	print "Generating meta-moderations...\n";

	my $t0 = new Benchmark;
	my $startcount = getM2ClosedCount(); 
	my($curcount, $m2count) = (0, 0);
	do {
		my $m2user = $slashdb->getUser(getRandomUID());
		my $comments = $slashdb->getMetamodComments(
			$m2user,
			$constants->{m2_comments}
		);

		for (@{$comments}) {
			# Our M2 "vote" which is randomly determined with a 
			# heavy bias towards "fair".
			my $val = (rand 11 > 1) ? '+' : '-';
			$slashdb->sqlInsert("metamodlog", {
				-mmid => $_->{id},
				-uid  => $m2user->{uid},
				-val  => ($val eq '+') ? '+1' : '-1',
				-ts   => "now()",
				-flag => 10,
			});
			$slashdb->sqlUpdate('moderatorlog', {
				-m2count => 'm2count+1',
			}, "id=$_->{id}");

			$m2count++;
			printf <<EOT, $curcount-$startcount, $opts{num_closed};
M2($m2count): ID #$_->{id}, M2Mod = $m2user->{uid}, Val = $val, C=%d/%d 
EOT

		}

		$curcount = getM2ClosedCount();
	} while ($curcount - $startcount < $opts{num_closed});
	my $t1 = new Benchmark;
	printf <<EOT, timestr(timediff($t1, $t0), 'noc');
$m2count meta-moderations (closing $opts{num_closed} mods) created in:
%s
EOT

}


# subroutines

sub getM2ClosedCount {
	return $slashdb->sqlSelect(
		'count(*)',
		'moderatorlog',
		"m2count >= $constants->{m2_consensus}"
	);
}

# CLOSURE!
{
my($uid_list);
sub getRandomUID {
	if (!$uid_list) {
		my $max_uid = int(
			$slashdb->getLastUser() *
			$constants->{m2_userpercentage}
		);
		$uid_list = $slashdb->sqlSelectAll(
			'users.uid',
			'users, users_prefs, users_info',
			"karma > 0 AND willing > 0 AND users.uid < $max_uid AND
			users.uid=users_prefs.uid AND users.uid=users_info.uid
			AND users.uid!=$constants->{anonymous_coward_uid}"
		);
		$_ = $_->[0] for @{$uid_list};
	}
	return $uid_list->[int rand($#{$uid_list} + 1)];
} }


sub usage {
	print "*** $_[0]\n" if $_[0];
	# Remember to doublecheck these match getopts()!
	print <<EOT;

Usage: $PROGNAME [OPTIONS] [# of moderations to close]

This utility creates test moderations for a given Slash site. This program is for
testing purposes, only, particularly for those ambitious Slash users out there
who want to try their hand at modifying the comment or moderation systems.

This program is *not* designed to work for large sites, just small test sites
that have been recently installed.

DO NOT USE THIS ON A PRODUCTION SITE.

You have been warned. :)

Main options:
	-h	Help (this message)
	-v	Version
	-u	Virtual user (default is "slash")
EOT

	exit;
}


sub version {
	print <<EOT;

$PROGNAME $VERSION

This code is a part of Slash, and is released under the GPL.
Copyright 1997-2001 by Open Source Development Network. See README
and COPYING for more information, or see http://slashcode.com/.

EOT
	exit;
}

__END__
