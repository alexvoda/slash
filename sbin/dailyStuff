#!/usr/bin/perl -w

###############################################################################
# dailyStuff - this program runs various housekeeping tasks, sends out the 
# the mailing list, and compiles site statistics report and sends the report 
# to the site admin 
#
# Copyright (C) 1997 Rob "CmdrTaco" Malda
# malda@slashdot.org
#
# This program is free software; you can redistribute it and/or
# modify it under the terms of the GNU General Public License
# as published by the Free Software Foundation; either version 2
# of the License, or (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA  02111-1307, USA.
#
#
#  $Id$
###############################################################################
use strict;
use File::Basename;
use File::Path;
use Slash;
use Slash::Utility;

my $constants = getCurrentStatic();
my $slashdb = getCurrentDB();

my $count = $slashdb->countDaily();

my $sdTotalHits = $slashdb->getVar("totalhits");

$sdTotalHits = $sdTotalHits + $count->{'total'};
$slashdb->setVar("totalhits", $sdTotalHits);

$slashdb->updateStamps();

my $email = <<EOT;
$constants->{sitename} Stats for yesterday

     total $count->{'total'}
    unique $count->{'unique'}
total hits $sdTotalHits
  homepage $count->{'index'}es{'index'}
   indexes 
EOT

for (keys %{$count->{'index'}}) {
	$email .= "\t   $_=$count->{'index'}es{$_}\n"
}

$email .= "\n-----------------------\n";

for my $key (sort { $count->{'articles'}{$b} <=> $count->{'articles'}{$a} } keys %{$count->{'articles'}}) {
	my $value = $count->{'articles'}{$key};

	my $story = $slashdb->getStory($key, 'title', 'aid');

	$email .= "$value\t$key " . substr($story->{'title'}, 0, 30) .
		" by $story->{'author'}\n" if $value > 100;
}

$email .= "\n-----------------------\n";
#$email .= `$constants->{slashdir}/bin/tailslash $virtual_user today`;
$email .= "\n-----------------------\n";


my $anonymous_coward_uid = getCurrentStatic('anonymous_coward_uid');
my $comlim = $slashdb->getUser($anonymous_coward_uid, 'commentlimit');
$slashdb->setUser($anonymous_coward_uid, { commentlimit => 50000 });
$email .= "Archiving Comments...\n";

# Find all stories over $constants->{archive_delay} days where writestatus != 5
my $columns = "sid,time,section,title";
my $tables = "stories";
my $where = "writestatus<5 AND writestatus >= 0 AND to_days(now()) - to_days(time) > $constants->{archive_delay}";

my $E = $slashdb->getOldStories($constants->{archive_delay});

for (@{$E}) {
	my($sid, $date, $section, $title) = ($_->[0], $_->[1], $_->[2], $_->[3]);
	$email.= "archiving: $sid $title \n";
	prog2file("$constants->{basedir}/article.pl sid=$sid\\\&ssi=yes\\\&mode=archive",
		"$constants->{basedir}/$section/$sid"."_F.shtml");

	unlink("$constants->{basedir}/$section/$sid.shtml")
		or warn "Can't unlink $constants->{basedir}/$section/$sid.shtml: $!";
	symlink("$constants->{basedir}/$section/${sid}_F.shtml", "$constants->{basedir}/$section/$sid.shtml")
		or die "Can't symlink $constants->{basedir}/$section/${sid}_F.shtml -> $sid.shtml";
	$slashdb->deleteComment($sid);
}
$slashdb->setUser($anonymous_coward_uid, { commentlimit=>$comlim });


sub generateDailyMail {
	my $email = $slashdb->getDailyEmail();

	my $r = "$constants->{sitename} Daily Headline Mailer\n\n";
	$r .= $slashdb->getBlock("emailsponsor");
	# newlines after advert 
	$r .= "\n\n";

	return unless @$email;
	for (@{$email}) {
		my($sid, $title, $section, $aid, $tid, $time, $dept) = @$_;
		$dept = $constants->{use_dept} ? "\n     from the $dept dept." : "";

		$r .= <<EOT;
$title$dept
     posted by $aid on $time ($tid)
     $constants->{rootdir}/article.pl?sid=$sid
EOT
	}
	return $r;
}

sub mailingList {
	my $p = generateDailyMail() or return;

	my $users = $slashdb->getMailingList();

	for (@{$users}) {
		sendEmail($_->[0], "24 Hours of $constants->{sitename} Headlines For $_->[2]", <<EOT);
      $p
      You are getting this message because you subscribed to it.
      If you want to unsubscribe from this, go to
      	$constants->{absolutedir}/users.pl
      You can login as "$_->[2]" and disable the mailing from there.
EOT
	}
}

sub prog2file {
	my($c, $f) = @_;

	my $d = `$c`;
	my $dir = dirname($f);
	mkpath($dir, 0, 0755) unless -e $dir;
	if (length($d) > 0) {
		local *F;
		open F, ">$f" or die "Can't open $f: $!";
		print F $d;
		close F;
		return "1";

	} else {
		return "0";
	}
}

# Send a message to the site admin.
if ($constants->{send_mail} == 1) {
	for (keys %{$constants->{stats_reports}}) {
		sendEmail($_, $constants->{stats_reports}{$_}, $email);
	}
	mailingList();
}

